// jukeboxclient
//
// Author: Fannybaws Tunwarm
// Copyright 2010,2011 - Fannybaws Tunwarm
// This script may be freely copied and modified so long as this header
// remains unmodified.
// 
// v2.0 - Initial upload to github
// renamed to jukeboxclient and now using jukeboxserver.php server side
// Will need customised versions for each user as serverkey is unique to them
//
// v1.98
// Now using test12.php
//
// v1.97
// Now using latest php file on server
//
// v1.96
// Added a check to look for AUTH returned from the server if key is invalid or missing
// and let the owner know what the problem is.
// 
// v1.95
// Adding a key to the parameter list that will be checked for server side to prevent unuathorised use
// 
// v1.94
// No longer need the chat_listen global
// 
// v1.93
// Removed dump_spacers() from timer() to stop spam to owner every time track changed 
// 
// v1.92
// Changing llSay to llOwnerSay and cleaning up output
// 
// v1.91
// Added checks to touch_start so that only the owner sees the dialog
// If any other user tries to touch it, they will receive an IM telling them that only owner can use the object
// 
// v1.9
// Changing the 'Change' option to 'Reset' so that it reads the folders again (useful if script has been running
// a while.
// 
// v1.8
// First attempt at clean-up, removing old chat commands
// Now removing error checking from some functions - shouldn't be needed if dialogs are done right :)
// Now removing some debug chat
// TODO - Do we need the playing integer?
// 
// v1.7
// Added Change button to Dialog (to change folder) but doesn't do anything yet
// 
// Added Code for the Playlist button to dump the playlist
// v1.6
// Added code to show folder buttons if box is touched when no stream loaded
// Clicking on the buttons should load and play the folder :)
//
// v1.5
// Semi-Working with Play, Stop and Next on Dialog
// Moved error checking from the listen event into function next_track()
// Starting to add some stuff in preparation for dialog box
// 
// v1.4
// Added dump_spacers function and used it in a few places to make output easier to read
// Added nothing_loaded() function to try and clean up and standardise error messages
// Amended some parts to show some suggestions when nothing loaded (play, next)
// 
// Changed the part in listen to specifically need 'folder=' before the folder name
// Hopefully this will stop some buggy stuff when unknown commands actually change the folder name even though folder not found.
// Typing anything not recognised as a command will now show an error message and the help message :)
//
// Created function stop_music() to move the code for stop command into there
// Added function dump_help and code in the listen event to liston for owner saying help
// Added code in http_response to check for first 4 characters of the returned string to be LIST or FOLD
// Added code to listen event to check for the folderlist command
//
// v1.3
// Added dump_playlist function and code to check for the 'playlist' command :)
// Removed default value for global var folder - it wasn't used anywhere
// Added code to listen to jump to next track
//
// v1.2
// Automatically play when new folder loaded successfully
// Added error checking when looking for folder - message given when folder not found
// Moved duplicate code to play the track into play_stream() [no paramaters required for play_stream now]
// Moved code to jump to next track from timer event into new function next_track()
// (Above will allow a future user triggered next track event)
//
// v1.1
// Removed some old commented code and stopped saying the filelist in chat when changing folder
// Created chat_listen global instead of hard-coding the listen channel
// Building on experimental release of 1.0

// Try making these global

list streams = [];
integer list_length;
list folders_list = [];    
integer folders_list_length;
key http_request_id;
string url = "http://opensim.brillyunt.com/jukeboxserver.php?serverkey=xxxxxxxxxxx";
//url = url + "?serverkey=" + serverkey;
string folder = "";
integer act_title = 0;
string base_path = "http://opensim.brillyunt.com/mp3/";
integer playing = 0;
integer authorised = 0;

// Setting up some stuff for dialog box
list dialog_choices = [];
string msg = "Please make a choice.";
key ToucherID;
integer channel_dialog;
integer listen_id;



// Declare a function to dump a couple of spacer lines
dump_spacers()
{
    llOwnerSay(".");
    llOwnerSay(".");
}

// Declare a function to stop the music
stop_music()
{
    // llSay(0, "Stopping Music");
    llSetTimerEvent(0);
    llSetParcelMusicURL("");
    playing = 0;
}

// Declare a function to dump the playlist
dump_playlist()
{
    dump_spacers();
    llOwnerSay("Current folder is " + folder);
    llOwnerSay((string)(list_length / 3) + " Songs in current playlist:");
    integer count = 0;
    while (count < list_length)
    {
        string this_song = llList2String(streams, count);
        this_song = right(this_song, "!");
        // Is this the current song?
        if (count == act_title)
        {
            this_song = "(Current) " + this_song;
        }
        string this_minutes = llList2String(streams, count + 1);
        string this_seconds = llList2String(streams, count + 2);
        llOwnerSay(this_song + " (" + this_minutes + ":" + this_seconds + ")");
        count += 3;
    }
}

// Declare a function to jump to the next track
next_track()
{
    // llSay(0, "Skipping to next track...");
    llSetTimerEvent(0);
    act_title += 3; // raise the title-counter;
    if(act_title >= list_length)
    { 
        // We reached the end of the list -> start over
        act_title = 0;
    }
    stop_music();
    play_stream();
}

// Declare a function to play the stream
play_stream()
{
    playing = 1;
    string song_url = llList2String(streams, act_title);
    song_url = right(song_url, "!");
    song_url = base_path + folder + "/" + song_url;
    float minutes = llList2Float(streams, act_title + 1);
    float seconds = llList2Float(streams, act_title + 2);
    float time = (minutes * 60) + seconds;
    llSetParcelMusicURL(song_url); // Set the Parcel-Music-Url
    llSetTimerEvent(time);  // Set the Timer-Event to the end of the tune
}

// Function to return string to the right of the our divider in the list("!")
string right(string src, string divider) {
    integer index = llSubStringIndex( src, divider );
    if(~index)
        return llDeleteSubString( src, 0, index + llStringLength(divider) - 1);
    return src;
}

default
{
    state_entry()
    {
        // For dialogue box
        channel_dialog = ( -1 * (integer)("0x"+llGetSubString((string)llGetKey(),-5,-1)) );
        
    }
    touch_start(integer total_number)
    {
        // Who touched me?
        ToucherID = llDetectedKey(0);
        
        // Check for owner touch only
        if (llDetectedOwner(0) == llGetOwner())
        {
            // What buttons should we show?
            if (list_length > 1)
            {
                // We have a playlist :)
                // So assume already authorised
                if (playing == 1)
                {
                    dialog_choices = ["Stop"];
                }
                else
                {
                    dialog_choices = ["Play"];
                }
                msg = "Please make your choice";
                dialog_choices = dialog_choices + ["Next", "Playlist", "Reset"];
            }
            else
            {
                // Nothing loaded so lets get the folders
                if (folders_list_length == 0)
                {
                    http_request_id = llHTTPRequest(url + "&list=1", [], "");
                    llOwnerSay("Attempting to connect and load folders, touch me again");
                    // msg = "Attempting to load folders...\nPlease click OK then touch the jukebox again";
                    // dialog_choices = ["OK"];
                }
                else
                {
                    msg = "Please choose a folder to load";
                    dialog_choices = folders_list;
                }
            }
            if (authorised == 1)
            {
                llDialog(ToucherID, msg, dialog_choices, channel_dialog);
                listen_id = llListen( channel_dialog, "", ToucherID, "");
            }
            else
            {
                // Problem with serverkey, we are not authorised so don't do menu
            }
        }
        else
        {
            llInstantMessage(ToucherID, "Sorry, only the owner can use this object");
        }
    }
    
    listen( integer channel, string name, key id, string message )
    {
        // What channel are we on?
        if (channel == channel_dialog)
        {
            if (message == "Stop")
            {
                // Stop music
                stop_music();
            }
            else if (message == "Next")
            {
                // Next track
                next_track();
            }
            else if (message == "Play")
            {
                // Play it
                play_stream();
            }
            else if (message == "Playlist")
            {
                // Dump the Playlist
                dump_playlist();
            }
            else if (message == "OK")
            {
                // Ignore this - its the first instance of touch with nothing loaded, folders are still loading
            }
            else if (message == "Reset")
            {
                // Reset the script (to allow loading new folder)
                llOwnerSay("Reset Clicked, touch again for updated folder list");
                stop_music();
                llResetScript();
                
            }
            else
            {
                // If we get here it must be a folder?
                
                llOwnerSay("Looking for " + message + " folder");
                llOwnerSay(url + "&folder=" + message);
                folder = message;
                http_request_id = llHTTPRequest(url + "&folder=" + message, [], "");
                
            }
        }
    }

    // Now for the timer
    timer()
    {
        // dump_spacers();
        next_track();
    }

    http_response(key request_id, integer status, list metadata, string body)
    {
        if (request_id == http_request_id)
        {
            if ( llGetSubString(body, 0, 3) == "AUTH" )
            {
                // Invalid or missing serverkey
                llOwnerSay("Sorry, you are not authorised to use this server!");
                authorised = 0;
                return;
            }
            else
            {
                authorised = 1;
            }
        
            // Remove newline characters
            body = (string)llParseString2List( body, ["\\n"], [] );
            if ( llSubStringIndex(body , "No such file or directory") != -1 )
            {
                llOwnerSay("Folder '" + folder + "' not found!");
            }
            else if ( llGetSubString(body, 0, 3) == "LIST" )
            {
                body = llGetSubString(body, 4, -1);
                folders_list = llParseString2List(body,[","],[]);
                folders_list_length = llGetListLength(folders_list);
            }
            else if ( llGetSubString(body, 0, 3) == "FOLD" )
            {
                body = llGetSubString(body, 4, -1);
                // Reset streams list to empty
                streams = [];
                act_title = 0;
                streams = streams + llParseString2List(body,[","],[]);
    
                list_length = llGetListLength(streams);
                llOwnerSay("Setting the playlist...");
                llSetTimerEvent(0);
                play_stream();
            }
            
            else
            {
                llOwnerSay("Unknown Error");
            }
        }
    }
}